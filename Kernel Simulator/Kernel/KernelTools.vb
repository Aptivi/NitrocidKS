
'    Kernel Simulator  Copyright (C) 2018-2021  EoflaOE
'
'    This file is part of Kernel Simulator
'
'    Kernel Simulator is free software: you can redistribute it and/or modify
'    it under the terms of the GNU General Public License as published by
'    the Free Software Foundation, either version 3 of the License, or
'    (at your option) any later version.
'
'    Kernel Simulator is distributed in the hope that it will be useful,
'    but WITHOUT ANY WARRANTY; without even the implied warranty of
'    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
'    GNU General Public License for more details.
'
'    You should have received a copy of the GNU General Public License
'    along with this program.  If not, see <https://www.gnu.org/licenses/>.

Imports System.IO
Imports System.Threading
Imports Newtonsoft.Json.Linq
Imports SemanVer.Instance

Public Module KernelTools

    ' A dictionary for storing paths and files (used for mods, screensavers, etc.)
    Public paths As New Dictionary(Of String, String)
    Friend RPCPowerListener As New Thread(AddressOf PowerManage) With {.Name = "RPC Power Listener Thread"}

    ' ----------------------------------------------- Kernel errors -----------------------------------------------

    ''' <summary>
    ''' Indicates that there's something wrong with the kernel.
    ''' </summary>
    ''' <param name="ErrorType">Specifies whether the error is serious, fatal, unrecoverable, or double panic. C/S/D/F/U</param>
    ''' <param name="Reboot">Specifies whether to reboot on panic or to show the message to press any key to shut down</param>
    ''' <param name="RebootTime">Specifies seconds before reboot. 0 is instant. Negative numbers are not allowed.</param>
    ''' <param name="Description">Explanation of what happened when it errored.</param>
    ''' <param name="Exc">An exception to get stack traces, etc. Used for dump files currently.</param>
    ''' <param name="Variables">Optional. Specifies variables to get on text that will be printed.</param>
    Public Sub KernelError(ErrorType As Char, Reboot As Boolean, RebootTime As Long, Description As String, Exc As Exception, ParamArray Variables() As Object)
        Try
            'Unquiet
            If EnteredArguments IsNot Nothing Then
                If EnteredArguments.Contains("quiet") Then
                    Wdbg("I", "Removing quiet...")
                    Console.SetOut(DefConsoleOut)
                End If
            End If

            'Check error types and its capabilities
            Wdbg("I", "Error type: {0}", ErrorType)
            If ErrorType = "S" Or ErrorType = "F" Or ErrorType = "U" Or ErrorType = "D" Or ErrorType = "C" Then
                If ErrorType = "U" And RebootTime > 5 Or ErrorType = "D" And RebootTime > 5 Then
                    'If the error type is unrecoverable, or double, and the reboot time exceeds 5 seconds, then
                    'generate a second kernel error stating that there is something wrong with the reboot time.
                    Wdbg("W", "Errors that have type {0} shouldn't exceed 5 seconds. RebootTime was {1} seconds", ErrorType, RebootTime)
                    KernelError("D", True, 5, DoTranslation("DOUBLE PANIC: Reboot Time exceeds maximum allowed {0} error reboot time. You found a kernel bug."), Nothing, CStr(ErrorType))
                    StopPanicAndGoToDoublePanic = True
                ElseIf ErrorType = "U" And Reboot = False Or ErrorType = "D" And Reboot = False Then
                    'If the error type is unrecoverable, or double, and the rebooting is false where it should
                    'not be false, then it can deal with this issue by enabling reboot.
                    Wdbg("W", "Errors that have type {0} enforced Reboot = True.", ErrorType)
                    Write(DoTranslation("[{0}] panic: Reboot enabled due to error level being {0}."), True, ColTypes.Uncontinuable, ErrorType)
                    Reboot = True
                End If
                If RebootTime > 3600 Then
                    'If the reboot time exceeds 1 hour, then it will set the time to 1 minute.
                    Wdbg("W", "RebootTime shouldn't exceed 1 hour. Was {0} seconds", RebootTime)
                    Write(DoTranslation("[{0}] panic: Time to reboot: {1} seconds, exceeds 1 hour. It is set to 1 minute."), True, ColTypes.Uncontinuable, ErrorType, CStr(RebootTime))
                    RebootTime = 60
                End If
            Else
                'If the error type is other than D/F/C/U/S, then it will generate a second error.
                Wdbg("E", "Error type {0} is not valid.", ErrorType)
                KernelError("D", True, 5, DoTranslation("DOUBLE PANIC: Error Type {0} invalid."), Nothing, CStr(ErrorType))
                StopPanicAndGoToDoublePanic = True
            End If

            'Format the "Description" string variable
            Description = String.Format(Description, Variables)

            'Fire an event
            EventManager.RaiseKernelError(ErrorType, Reboot, RebootTime, Description, Exc, Variables)

            'Make a dump file
            GeneratePanicDump(Description, ErrorType, Exc)

            'Check error capabilities
            If Description.Contains("DOUBLE PANIC: ") And ErrorType = "D" Then
                'If the description has a double panic tag and the error type is Double
                Wdbg("F", "Double panic caused by bug in kernel crash.")
                Write(DoTranslation("[{0}] dpanic: {1} -- Rebooting in {2} seconds..."), True, ColTypes.Uncontinuable, ErrorType, Description, CStr(RebootTime))
                Thread.Sleep(RebootTime * 1000)
                Wdbg("F", "Rebooting")
                PowerManage("reboot")
                adminList.Clear()
                disabledList.Clear()
            ElseIf StopPanicAndGoToDoublePanic = True Then
                'Switch to Double Panic
                Exit Sub
            ElseIf ErrorType = "C" And Reboot = True Then
                'Check if error is Continuable and reboot is enabled
                Wdbg("W", "Continuable kernel errors shouldn't have Reboot = True.")
                Write(DoTranslation("[{0}] panic: Reboot disabled due to error level being {0}.") + vbNewLine +
                  DoTranslation("[{0}] panic: {1} -- Press any key to continue using the kernel."), True, ColTypes.Continuable, ErrorType, Description)
                Console.ReadKey()
            ElseIf ErrorType = "C" And Reboot = False Then
                'Check if error is Continuable and reboot is disabled
                EventManager.RaiseContKernelError(ErrorType, Reboot, RebootTime, Description, Exc, Variables)
                Write(DoTranslation("[{0}] panic: {1} -- Press any key to continue using the kernel."), True, ColTypes.Continuable, ErrorType, Description)
                Console.ReadKey()
            ElseIf (Reboot = False And ErrorType <> "D") Or (Reboot = False And ErrorType <> "C") Then
                'If rebooting is disabled and the error type does not equal Double or Continuable
                Wdbg("W", "Reboot is False, ErrorType is not double or continuable.")
                Write(DoTranslation("[{0}] panic: {1} -- Press any key to shutdown."), True, ColTypes.Uncontinuable, ErrorType, Description)
                Console.ReadKey()
                PowerManage("shutdown")
            Else
                'Everything else.
                Wdbg("F", "Kernel panic initiated with reboot time: {0} seconds, Error Type: {1}", RebootTime, ErrorType)
                Write(DoTranslation("[{0}] panic: {1} -- Rebooting in {2} seconds..."), True, ColTypes.Uncontinuable, ErrorType, Description, CStr(RebootTime))
                Thread.Sleep(RebootTime * 1000)
                PowerManage("reboot")
                adminList.Clear()
                disabledList.Clear()
            End If
        Catch ex As Exception
            'Check to see if it's a double panic
            If ErrorType = "D" Then
                'Trigger triple fault
                Wdbg("F", "TRIPLE FAULT: Kernel bug: {0}", ex.Message)
                WStkTrc(ex)
                Environment.FailFast("TRIPLE FAULT in trying to handle DOUBLE PANIC. KS can't continue.", ex)
            Else
                'Alright, we have a double panic.
                Wdbg("F", "DOUBLE PANIC: Kernel bug: {0}", ex.Message)
                WStkTrc(ex)
                KernelError("D", True, 5, DoTranslation("DOUBLE PANIC: Kernel bug: {0}"), ex, ex.Message)
            End If
        End Try
    End Sub

    ''' <summary>
    ''' Generates the stack trace dump file for kernel panics
    ''' </summary>
    ''' <param name="Description">Error description</param>
    ''' <param name="ErrorType">Error type</param>
    ''' <param name="Exc">Exception</param>
    Sub GeneratePanicDump(Description As String, ErrorType As Char, Exc As Exception)
        Try
            'Open a file stream for dump
            Dim Dump As New StreamWriter($"{paths("Home")}/dmp_{RenderDate(FormatType.Short).Replace("/", "-")}_{RenderTime(FormatType.Short).Replace(":", "-")}.txt")
            Wdbg("I", "Opened file stream in home directory, saved as dmp_{0}.txt", $"{RenderDate(FormatType.Short).Replace("/", "-")}_{RenderTime(FormatType.Short).Replace(":", "-")}")

            'Write info (Header)
            Dump.AutoFlush = True
            Dump.WriteLine(DoTranslation("----------------------------- Kernel panic dump -----------------------------") + vbNewLine + vbNewLine +
                           DoTranslation(">> Panic information <<") + vbNewLine +
                           DoTranslation("> Description: {0}") + vbNewLine +
                           DoTranslation("> Error type: {1}") + vbNewLine +
                           DoTranslation("> Date and Time: {2}") + vbNewLine, Description, ErrorType, Render)

            'Write Info (Exception)
            If Exc IsNot Nothing Then
                Dim Count As Integer = 1
                Dump.WriteLine(DoTranslation(">> Exception information <<") + vbNewLine +
                               DoTranslation("> Exception: {0}") + vbNewLine +
                               DoTranslation("> Description: {1}") + vbNewLine +
                               DoTranslation("> HRESULT: {2}") + vbNewLine +
                               DoTranslation("> Source: {3}") + vbNewLine + vbNewLine +
                               DoTranslation("> Stack trace <") + vbNewLine + vbNewLine +
                               Exc.StackTrace + vbNewLine + vbNewLine +
                               DoTranslation(">> Inner exception {0} information <<"), Exc.ToString.Substring(0, Exc.ToString.IndexOf(":")), Exc.Message, Exc.HResult, Exc.Source)

                'Write info (Inner exceptions)
                Dim InnerExc As Exception = Exc.InnerException
                While InnerExc IsNot Nothing
                    Dump.WriteLine(DoTranslation("> Exception: {0}") + vbNewLine +
                                   DoTranslation("> Description: {1}") + vbNewLine +
                                   DoTranslation("> HRESULT: {2}") + vbNewLine +
                                   DoTranslation("> Source: {3}") + vbNewLine + vbNewLine +
                                   DoTranslation("> Stack trace <") + vbNewLine + vbNewLine +
                                   InnerExc.StackTrace + vbNewLine, InnerExc.ToString.Substring(0, InnerExc.ToString.IndexOf(":")), InnerExc.Message, InnerExc.HResult, InnerExc.Source)
                    InnerExc = InnerExc.InnerException
                    If InnerExc IsNot Nothing Then
                        Dump.WriteLine(DoTranslation(">> Inner exception {0} information <<"), Count)
                    Else
                        Dump.WriteLine(DoTranslation(">> Exception {0} is the root cause <<"), Count - 1)
                    End If
                    Count += 1
                End While
                Dump.WriteLine()
            Else
                Dump.WriteLine(DoTranslation(">> No exception; might be a kernel error. <<") + vbNewLine)
            End If

            'Write info (Frames)
            Dump.WriteLine(DoTranslation(">> Frames, files, lines, and columns <<"))
            Try
                Dim ExcTrace As New StackTrace(Exc, True)
                Dim FrameNo As Integer = 1
                For Each Frame As StackFrame In ExcTrace.GetFrames
                    If Not (Frame.GetFileName = "" And Frame.GetFileLineNumber = 0 And Frame.GetFileColumnNumber = 0) Then
                        Dump.WriteLine(DoTranslation("> Frame {0}: File: {1} | Line: {2} | Column: {3}"), FrameNo, Frame.GetFileName, Frame.GetFileLineNumber, Frame.GetFileColumnNumber)
                    End If
                    FrameNo += 1
                Next
            Catch ex As Exception
                WStkTrc(ex)
                Dump.WriteLine(DoTranslation("> There is an error when trying to get frame information. {0}: {1}"), ex.GetType.FullName.Substring(0, ex.GetType.FullName.IndexOf(":")), ex.Message.Replace(vbNewLine, " | "))
            End Try

            'Close stream
            Wdbg("I", "Closing file stream for dump...")
            Dump.Flush() : Dump.Close()
        Catch ex As Exception
            Write(DoTranslation("Dump information gatherer crashed when trying to get information about {0}: {1}"), True, ColTypes.Error, Exc.ToString.Substring(0, Exc.ToString.IndexOf(":")), ex.Message)
            WStkTrc(ex)
        End Try
    End Sub

    ' ----------------------------------------------- Power management -----------------------------------------------

    ''' <summary>
    ''' Manage computer's (actually, simulated computer) power
    ''' </summary>
    ''' <param name="PowerMode">Whether it would be "shutdown", "rebootsafe", or "reboot"</param>
    Public Sub PowerManage(PowerMode As String)
        PowerManage(PowerMode, "0.0.0.0", RPCPort)
    End Sub

    ''' <summary>
    ''' Manage computer's (actually, simulated computer) power
    ''' </summary>
    ''' <param name="PowerMode">Whether it would be "shutdown", "rebootsafe", or "reboot"</param>
    Public Sub PowerManage(PowerMode As String, IP As String)
        PowerManage(PowerMode, IP, RPCPort)
    End Sub

    ''' <summary>
    ''' Manage computer's (actually, simulated computer) power
    ''' </summary>
    ''' <param name="PowerMode">Whether it would be "shutdown", "rebootsafe", or "reboot"</param>
    Public Sub PowerManage(PowerMode As String, IP As String, Port As Integer)
        Wdbg("I", "Power management has the argument of {0}", PowerMode)
        If PowerMode = "shutdown" Then
            EventManager.RaisePreShutdown()
            Write(DoTranslation("Shutting down..."), True, ColTypes.Neutral)
            ResetEverything()
            EventManager.RaisePostShutdown()
            Environment.Exit(0)
        ElseIf PowerMode = "reboot" Then
            EventManager.RaisePreReboot()
            Write(DoTranslation("Rebooting..."), True, ColTypes.Neutral)
            ResetEverything()
            EventManager.RaisePostReboot()
            Console.Clear()
            RebootRequested = True
            LogoutRequested = True
            SafeMode = False
        ElseIf PowerMode = "rebootsafe" Then
            EventManager.RaisePreReboot()
            Write(DoTranslation("Rebooting..."), True, ColTypes.Neutral)
            ResetEverything()
            EventManager.RaisePostReboot()
            Console.Clear()
            RebootRequested = True
            LogoutRequested = True
            SafeMode = True
        ElseIf PowerMode = "remoteshutdown" Then
            SendCommand("<Request:Shutdown>(" + IP + ")", IP, Port)
        ElseIf PowerMode = "remoterestart" Then
            SendCommand("<Request:Reboot>(" + IP + ")", IP, Port)
        End If
    End Sub

    ' ----------------------------------------------- Init and reset -----------------------------------------------
    ''' <summary>
    ''' Reset everything for the next restart
    ''' </summary>
    Sub ResetEverything()
        'Reset every variable that is resettable
        If argsInjected = False Then
            EnteredArguments.Clear()
        End If
        StopPanicAndGoToDoublePanic = False
        strcommand = Nothing
        modcmnds.Clear()
        moddefs.Clear()
        FTPModCommands.Clear()
        FTPModDefs.Clear()
        SFTPModCommands.Clear()
        SFTPModDefs.Clear()
        MailModCommands.Clear()
        MailModDefs.Clear()
        RDebugModDefs.Clear()
        DebugModCmds.Clear()
        TestModDefs.Clear()
        Test_ModCommands.Clear()
        TextEdit_ModCommands.Clear()
        TextEdit_ModHelpEntries.Clear()
        ZipShell_ModCommands.Clear()
        ZipShell_ModHelpEntries.Clear()
        RSSModCommands.Clear()
        RSSModDefs.Clear()
        scripts.Clear()
        Aliases.Clear()
        RemoteDebugAliases.Clear()
        FTPShellAliases.Clear()
        SFTPShellAliases.Clear()
        MailShellAliases.Clear()
        Wdbg("I", "General variables reset")

        'Reset hardware info
        HardwareInfo = Nothing
        Wdbg("I", "Hardware info reset.")

        'Disconnect all hosts from remote debugger
        StartRDebugThread(False)
        Wdbg("I", "Remote debugger stopped")

        'Stop all mods
        StopMods()
        Wdbg("I", "Mods stopped")

        'Disable Debugger
        If DebugMode = True Then
            Wdbg("I", "Shutting down debugger")
            DebugMode = False
            dbgWriter.Close() : dbgWriter.Dispose()
        End If

        'Stop RPC
        RPCThread.Abort()
        RPCListen?.Close()
        RPCListen = Nothing
        RPCThread = New Thread(AddressOf RecCommand) With {.IsBackground = True, .Name = "RPC Thread"}

        'Disconnect from mail
        IMAP_Client.Disconnect(True)
        SMTP_Client.Disconnect(True)

        'Disable safe mode
        SafeMode = False
    End Sub

    ''' <summary>
    ''' Initializes everything
    ''' </summary>
    Sub InitEverything(Args() As String)
        'Initialize help
        InitHelp()
        InitFTPHelp()
        InitSFTPHelp()
        IMAPInitHelp()
        InitRDebugHelp()
        InitTestHelp()
        TextEdit_UpdateHelp()
        ZipShell_UpdateHelp()
        InitRSSHelp()

        'Initialize aliases
        InitAliases()

        'Initialize date
        If Not TimeDateIsSet Then
            InitTimeDate()
            TimeDateIsSet = True
        End If

        'Create config file and then read it
        InitializeConfig()

        'Load user token
        LoadUserToken()

        If RebootRequested Then
            Exit Sub
        End If

        'Show welcome message.
        If StartScroll Then
            WriteSlowlyC("      >> " + DoTranslation("Welcome to the kernel! - Version {0}") + " <<", True, 10, ColTypes.Banner, KernelVersion)
        Else
            Write("      >> " + DoTranslation("Welcome to the kernel! - Version {0}") + " <<", True, ColTypes.Banner, KernelVersion)
        End If

        'Show license
        Write(vbNewLine + "    Kernel Simulator  Copyright (C) 2018-2021  EoflaOE" + vbNewLine +
                      "    This program comes with ABSOLUTELY NO WARRANTY, not even " + vbNewLine +
                      "    MERCHANTABILITY or FITNESS for particular purposes." + vbNewLine +
                      "    This is free software, and you are welcome to redistribute it" + vbNewLine +
                      "    under certain conditions; See COPYING file in source code." + vbNewLine, True, ColTypes.License)

        'Some information
        WriteSeparator(DoTranslation("- App information"), False, ColTypes.Stage)
        Write("OS: " + DoTranslation("Running on {0}"), True, ColTypes.Neutral, Environment.OSVersion.ToString)

        'Show dev version notice
#If SPECIFIER = "DEV" Then 'WARNING: When the development nearly ends after "NEARING" stage, change the compiler constant value to "REL" to suppress this message out of stable versions
        Write(DoTranslation("Looks like you were running the development version of the kernel. While you can see the aspects, it is frequently updated and might introduce bugs. It is recommended that you stay on the stable version."), True, ColTypes.Neutral)
#ElseIf SPECIFIER = "RC" Then
        Write(DoTranslation("Looks like you were running the release candidate version. It is recommended that you stay on the stable version."), True, ColTypes.Neutral)
#End If

        'Parse real command-line arguments
        ParseCMDArguments(Args)

        'Check arguments
        If argsOnBoot Then
            StageTimer.Stop()
            PromptArgs()
            StageTimer.Start()
        End If
        If argsInjected Then
            argsInjected = False
            ParseArguments()
        End If

        'Write headers for debug
        Wdbg("I", "-------------------------------------------------------------------")
        Wdbg("I", "Kernel initialized, version {0}.", KernelVersion)
        Wdbg("I", "OS: {0}", Environment.OSVersion.ToString)

        'Populate ban list for debug devices
        PopulateBlockedDevices()

        'Start screensaver timeout
        If Not Timeout.IsBusy Then Timeout.RunWorkerAsync()
    End Sub

    ''' <summary>
    ''' Initializes the paths
    ''' </summary>
    Sub InitPaths()
        If IsOnUnix() Then
            paths.AddIfNotFound("Mods", Environ("HOME") + "/KSMods/")
            paths.AddIfNotFound("Configuration", Environ("HOME") + "/KernelConfig.json")
            paths.AddIfNotFound("Debugging", Environ("HOME") + "/kernelDbg.log")
            paths.AddIfNotFound("Aliases", Environ("HOME") + "/Aliases.json")
            paths.AddIfNotFound("Users", Environ("HOME") + "/Users.json")
            paths.AddIfNotFound("FTPSpeedDial", Environ("HOME") + "/FTP_SpeedDial.json")
            paths.AddIfNotFound("SFTPSpeedDial", Environ("HOME") + "/SFTP_SpeedDial.json")
            paths.AddIfNotFound("DebugDevNames", Environ("HOME") + "/DebugDeviceNames.json")
            paths.AddIfNotFound("MOTD", Environ("HOME") + "/MOTD.txt")
            paths.AddIfNotFound("MAL", Environ("HOME") + "/MAL.txt")
            paths.AddIfNotFound("CustomSaverSettings", Environ("HOME") + "/CustomSaverSettings.json")
            paths.AddIfNotFound("Home", Environ("HOME"))
            paths.AddIfNotFound("Temp", "/tmp")
        Else
            paths.AddIfNotFound("Mods", Environ("USERPROFILE").Replace("\", "/") + "/KSMods/")
            paths.AddIfNotFound("Configuration", Environ("USERPROFILE").Replace("\", "/") + "/KernelConfig.json")
            paths.AddIfNotFound("Debugging", Environ("USERPROFILE").Replace("\", "/") + "/kernelDbg.log")
            paths.AddIfNotFound("Aliases", Environ("USERPROFILE").Replace("\", "/") + "/Aliases.json")
            paths.AddIfNotFound("Users", Environ("USERPROFILE").Replace("\", "/") + "/Users.json")
            paths.AddIfNotFound("FTPSpeedDial", Environ("USERPROFILE").Replace("\", "/") + "/FTP_SpeedDial.json")
            paths.AddIfNotFound("SFTPSpeedDial", Environ("USERPROFILE").Replace("\", "/") + "/SFTP_SpeedDial.json")
            paths.AddIfNotFound("DebugDevNames", Environ("USERPROFILE").Replace("\", "/") + "/DebugDeviceNames.json")
            paths.AddIfNotFound("MOTD", Environ("USERPROFILE").Replace("\", "/") + "/MOTD.txt")
            paths.AddIfNotFound("MAL", Environ("USERPROFILE").Replace("\", "/") + "/MAL.txt")
            paths.AddIfNotFound("CustomSaverSettings", Environ("USERPROFILE").Replace("\", "/") + "/CustomSaverSettings.json")
            paths.AddIfNotFound("Home", Environ("USERPROFILE").Replace("\", "/"))
            paths.AddIfNotFound("Temp", Environ("TEMP").Replace("\", "/"))
        End If
    End Sub

    ''' <summary>
    ''' Fetches the GitHub repo to see if there are any updates
    ''' </summary>
    ''' <returns>A list which contains both the version and the URL</returns>
    Public Function FetchKernelUpdates() As List(Of String)
        Try
            Dim UpdateSpecifier As New List(Of String)
            Dim UpdateDown As New WebClient
            UpdateDown.Headers.Add(HttpRequestHeader.UserAgent, "EoflaOE") 'Because api.github.com requires the UserAgent header to be put, else, 403 error occurs.
            Dim UpdateStr As String = UpdateDown.DownloadString("https://api.github.com/repos/Aptivi/NitrocidKS/releases")
            Dim UpdateToken As JToken = JToken.Parse(UpdateStr)
            Dim tagName As String = UpdateToken.First.SelectToken("tag_name").ToString()
            tagName = If(tagName.StartsWith("v"), tagName.Substring(1), tagName)
            Dim UpdateVer As SemVer = Nothing
            If tagName.Split(".").Length > 3 Then
                UpdateVer = SemVer.ParseWithRev(tagName)
            Else
                UpdateVer = SemVer.Parse(tagName)
            End If
            Dim UpdateURL As String = UpdateToken.First.SelectToken("html_url")
            Dim CurrentVer As SemVer = SemVer.ParseWithRev(KernelVersion)
            If UpdateVer > CurrentVer Then
                'Found a new version
                UpdateSpecifier.Add(UpdateVer.ToString)
                UpdateSpecifier.Add(UpdateURL)
            End If
            Return UpdateSpecifier
        Catch ex As Exception
            Wdbg("E", "Failed to check for updates: {0}", ex.Message)
            WStkTrc(ex)
        End Try
        Return Nothing
    End Function

    Sub CheckKernelUpdates()
        Write(DoTranslation("Checking for system updates..."), True, ColTypes.Neutral)
        Dim AvailableUpdates As List(Of String) = FetchKernelUpdates()
        If AvailableUpdates IsNot Nothing AndAlso AvailableUpdates.Count > 0 Then
            Write(DoTranslation("Found new version: "), False, ColTypes.ListEntry)
            Write(AvailableUpdates(0), True, ColTypes.ListValue)
            Write(DoTranslation("You can download it at: "), False, ColTypes.ListEntry)
            Write(AvailableUpdates(1), True, ColTypes.ListValue)
        ElseIf AvailableUpdates Is Nothing Then
            Write(DoTranslation("Failed to check for updates."), True, ColTypes.Error)
        End If
    End Sub

    ''' <summary>
    ''' Removes all configuration files
    ''' </summary>
    Sub FactoryReset()
        For Each PathName As String In paths.Keys
            If Not PathName = "Home" And Not PathName = "Temp" Then
                If File.Exists(paths(PathName)) Then
                    File.Delete(paths(PathName))
                Else
                    Directory.Delete(paths(PathName), True)
                End If
            End If
        Next
        Environment.Exit(0)
    End Sub

    ''' <summary>
    ''' Polls $TERM_PROGRAM to get terminal emulator
    ''' </summary>
    Public Function GetTerminalEmulator() As String
        Return Environ("TERM_PROGRAM")
    End Function

    ''' <summary>
    ''' Gets the used compiler variables for building Kernel Simulator
    ''' </summary>
    ''' <returns>An array containing used compiler variables</returns>
    Public Function GetCompilerVars() As String()
        Dim CompilerVars As New List(Of String)

        'Determine the compiler vars used to build KS using conditional checks
#If NTFSCorruptionFix Then
        CompilerVars.Add("NTFSCorruptionFix")
#End If

#If NOWRITELOCK Then
        CompilerVars.Add("NOWRITELOCK")
#End If

#If SPECIFIER = "DEV" Then
        CompilerVars.Add("SPECIFIER = ""DEV""")
#ElseIf SPECIFIER = "RC" Then
        CompilerVars.Add("SPECIFIER = ""RC""")
#ElseIf SPECIFIER = "REL" Then
        CompilerVars.Add("SPECIFIER = ""REL""")
#End If

#If ENABLEIMMEDIATEWINDOWDEBUG Then
        CompilerVars.Add("ENABLEIMMEDIATEWINDOWDEBUG")
#End If

#If STOCKTERMINALMACOS Then
        CompilerVars.Add("STOCKTERMINALMACOS")
#End If

        'Return the compiler vars
        Return CompilerVars.ToArray
    End Function

End Module
